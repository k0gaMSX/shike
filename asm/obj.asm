;NOMBRE: PUT_OBJI
;OBJETIVO: PINTAR UN OBJETO DEL INVENTARIO
;ENTRADA: A -> NUMERO DE OBJETO
;         C -> CODIGO DE OBJETO


PUT_OBJI:	
	LD	HL,SHOWINVB

	EX	AF,AF'
	LD	A,C
	CP	-1
	RET	Z
	EX	AF,AF'

	ADD	A,A
	LD	E,A
	LD	D,0
	ADD	HL,DE

	LD	A,(HL)
	LD	(PUT_OBJIE+4),A
	INC	HL
	LD	A,(HL)
	LD	(PUT_OBJIE+6),A


	BIT	7,C
	LD	A,16
	JR	Z,PUT_OBJI1
	LD	A,8

PUT_OBJI1	LD	(PUT_OBJIE+8),A
	LD	(PUT_OBJIE+10),A


	LD	D,0
	LD	E,C
	LD	HL,STRUCTOBJETO
	CALL	MULTHLDE	;DIRECCIONAMOS EL OBJETO
	LD	DE,OBJ_VEC
	ADD	HL,DE
	LD	DE,STROBJ_GRAF
	ADD	HL,DE	;EL CAMPO GRAFICO

	LD	A,(HL)
	SLA	A
	LD	C,A
	LD	HL,GRAF_OBJ
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	INC	HL
	LD	(PUT_OBJIE+0),A
	LD	A,(HL)
	INC	HL
	LD	(PUT_OBJIE+2),A

	LD	A,(PAG_NACT)
	LD	(PUT_OBJIE+7),A

	CALL	WAIT_COM
	LD	HL,PUT_OBJIE
	CALL	COPYVRAM
	RET



PUT_OBJIE:	DB	0,0,0,PAG_OBJETOS,0,0,0,0,0,0,0,0,0,0,98h


;NOMBRE: GEST_INV
;OBJETIVO: ESTA FUNVION MUESTRA Y PERMITE MODIFICAR EL INVENTARIO DE UN
;          PERSONAJE
;ENTRADA: A -> NUMERO DEL PERSONAJE

GEST_INV:	
	PUSH	AF
	CALL	MAKE_INV
	POP	AF
	CALL	SHOW_INV

	LD	DE,128*256+30	;SE SUPONE
	LD	(CRUZ_Y),DE
	LD	A,1
	LD	(MOV_DIG),A

GEST_INV1	XOR	A
	LD	(BUFFER_FUNC),A
	EI
	HALT
	LD	DE,(CRUZ_Y)
	LD	C,SPRITE_CRUZ
	LD	B,2
	CALL	GOTO_SPXY
	CALL	SPD_ON
	LD	HL,CRUZ_Y
	CALL	MOVE_XX
	CALL	ASK_OUT

	LD	A,F1_KEY
	CALL	TEST_FUNC
	OR	A
	JR	Z,GEST_INV0
	XOR	A
	LD	(MOV_DIG),A
	RET

GEST_INV0	LD	A,SPC_KEY
	CALL	TEST_FUNC
	OR	A
	JR	Z,GEST_INV1

	LD	HL,(CRUZ_Y)
	CALL	CHECK_CINV	;AHORA TENGO QUE COMPROBAR SI
	LD	(CAS_ORI),A
	OR	A
	JR	Z,GEST_INV1
	PUSH	AF
	PUSH	DE
	CALL	SPD_OFF
	POP	DE

	LD	B,2
	LD	A,(PAG_ACT)
GEST_INVB	PUSH	BC
	LD	B,A
	EX	DE,HL
	PUSH	HL
	LD	D,(HL)
	INC	HL
	LD	E,(HL)
	EX	DE,HL
	LD	E,16
	LD	D,E
	CALL	PAINT_BALD
	POP	DE
	POP	BC
	LD	A,(PAG_NACT)
	DJNZ	GEST_INVB

	LD	HL,(CRUZ_Y)
	LD	(COOR_Y_NA),HL

	POP	AF
	CALL	MOVE_OBJ
	JR	GEST_INV1




;NOMBRE: MOVE_OBJ
;OBJETIVO: MOVER UN OBJETO POR LA PANTALLA PARA COLOCARLO EN EL LUGAR DESEADO
;ENTRADA: (EQUIPO_ACT) -> VARIABLE QUE INDICA EL EQUIPO DEL PERSONAJE ACTUAL
;         (INV_ACT) -> VARIABLE QUE INDICA EL INVENTARIO DEL PERSONAJE ACTUAL
;         A -> NUMERO DE LA CASILLA DONDE SE ENCUENTRA EL OBJETO QUE SE
;              DESEA MOVER



MOVE_OBJ:	LD	B,8
	LD	HL,(INV_ACT)
	CP	B
	JR	NC,MOVE_OBJ0
	LD	HL,(EQUIPO_ACT)
	LD	B,0



MOVE_OBJ0:	SUB	B
	LD	E,A
	LD	D,0
	ADD	HL,DE	;YA TENGO EL OBJETO!!!!!
;                                       ;Y AHORA A MOVERLO
	LD	A,(HL)
	CP	-1
	RET	Z

	LD	(NUM_OBJ),A


	LD	L,A
	LD	H,0
	LD	DE,STRUCTOBJETO
	CALL	MULTHLDE
	LD	DE,OBJ_VEC
	ADD	HL,DE
	LD	DE,STROBJ_GRAF
	ADD	HL,DE
	LD	A,(HL)
	SLA	A
	LD	E,A
	LD	D,0
	LD	HL,GRAF_OBJ
	ADD	HL,DE
	LD	A,(HL)

	LD	(GEST_DATAP),A
	INC	HL
	LD	A,(HL)
	LD	(GEST_DATAP+2),A


	CALL	WRITE_PNA_C
	CALL	SWAP_PAGE_C


MOVE_OBJ1	XOR	A
	LD	(BUFFER_FUNC),A
	CALL	WRITE_PNA_C
	CALL	SWAP_PAGE_C
	CALL	ERASE_PNA_C
	CALL	ASK_OUT

	LD	A,SPC_KEY
	CALL	TEST_FUNC
	OR	A
	JR	Z,MOVE_OBJ1


	LD	HL,(CRUZ_Y)
	CALL	CHECK_CINV	;AHORA TENGO QUE COMPROBAR SI
	OR	A
	JR	Z,MOVE_OBJ1


	LD	A,C
	LD	(NUM_CAS),A
	LD	(CAS_RES),A
	LD	HL,NUM_OBJ
	LD	C,(HL)
	CALL	ASSIGN_OBJ
	LD	A,-1
	CP	B
	JR	NZ,GEST_INP
	LD	A,(CAS_ORI)
	LD	(CAS_RES),A

GEST_INP	LD	A,(NUM_OBJ)
	LD	C,A
	LD	A,(CAS_RES)
	CALL	PUT_OBJI

	CALL	SWAP_PAGE_C
	CALL	ERASE_PNA_C

	LD	A,(NUM_OBJ)
	LD	C,A
	LD	A,(CAS_RES)
	CALL	PUT_OBJI
	RET





CAS_RES:	DB	0
NUM_CAS:	DB	0
NUM_OBJ:	DB	0
CAS_ORI:	DB	0


;NOMBRE: ASSIGN_OBJ
;OBJETIVO: ASIGNAR UN OBJETO A UN LUGAR DEL INVENTARIO O DEL EQUIPO
;ENTRADA:  A  -> CASILLA
;          C  -> OBJETO
;         (EQUIPO_ACT) -> VARIABLE QUE INDICA EL EQUIPO DEL PERSONAJE ACTUAL
;         (INV_ACT) -> VARIABLE QUE INDICA EL INVENTARIO DEL PERSONAJE ACTUAL


ASSIGN_OBJ:	LD	B,8
	LD	HL,(INV_ACT)
	LD	D,1
	CP	B
	JR	NC,ASSIGN_I0	;ASIGNACION AL EQUIPO
	LD	B,0
	LD	HL,(EQUIPO_ACT)
	DEC	D

ASSIGN_I0	SUB	B
	LD	E,A
	LD	A,D
	LD	(ASSIGN_FLG),A	;FLAG PARA VER DONDE SE PONE EL OBJETO
	LD	D,0
	ADD	HL,DE

	LD	B,-1
	LD	A,(HL)
	CP	B
	RET	NZ

	LD	A,(ASSIGN_FLG)
	OR	A
	JR	NZ,ASSIGN_I2

	PUSH	HL
	PUSH	BC
	CALL	CHK_ASG_OBJ
	POP	BC
	POP	HL

	LD	B,-1
	OR	A
	RET	NZ


ASSIGN_I2	LD	(HL),C
	LD	HL,(INV_ACT)
	XOR	A
	LD	(ASSIGN_FLG),A
	LD	A,(CAS_ORI)


	LD	C,8
	CP	C
	JR	NC,ASSIG_I1
	LD	C,0
	LD	HL,(EQUIPO_ACT)
	EX	AF,AF'
	LD	A,1
	LD	(ASSIGN_FLG),A
	EX	AF,AF'


ASSIG_I1	SUB	C
	LD	E,A
	LD	D,0
	ADD	HL,DE
	LD	C,(HL)
	LD	(HL),B

	LD	A,(ASSIGN_FLG)
	OR	A
	JR	Z,ASSIG_RET
	CALL	QUIT_OBJ

ASSIG_RET	LD	B,0
	RET


ASSIGN_FLG:	DB	0



;NOMBRE: CHK_ASG_OBJ
;OBJETIVO: COMPROBAR LAS ASIGNACIONES DE OBJETOS AL EQUIPO
;ENTRADA   CASILLA DE ASIGNACION
;          C  -> OBJETO
;         (EQUIPO_ACT) -> VARIABLE QUE INDICA EL EQUIPO DEL PERSONAJE ACTUAL
;         (INV_ACT) -> VARIABLE QUE INDICA EL INVENTARIO DEL PERSONAJE ACTUAL



CHK_ASG_OBJ:	LD	D,0
	LD	E,C
	LD	HL,STRUCTOBJETO
	CALL	MULTHLDE
	LD	DE,OBJ_VEC
	ADD	HL,DE
	LD	DE,STROBJ_LG
	EX	DE,HL
	ADD	HL,DE

	LD	A,(NUM_CAS)
	LD	B,(HL)

	LD	L,1

CHK_AO1	OR	A
	JR	Z,CHK_A01
	SLA	L
	DEC	A
	JR	CHK_AO1


CHK_A01	LD	A,B
	AND	L
	LD	A,0FFh
	RET	Z	;A CONTINUACION VIENE LA COMPROBACION

	LD	IX,(INV_PERS)	;ESTO CREO QUE SOBRA
	LD	IY,(INV_TIPO)
	LD	HL,STROBJ_AT
	ADD	HL,DE
	LD	A,(HL)
	ADD	A,(IX+STRPER_PVTQ)
	LD	(IX+STRPER_PVTQ),A
	INC	HL
	LD	A,(HL)
	ADD	A,(IX+STRPER_PAR)
	LD	(IX+STRPER_PAR),A
	INC	HL
	LD	A,(HL)
	ADD	A,(IX+STRPER_PMV)
	LD	(IX+STRPER_PMV),A
	INC	HL
	LD	A,(HL)
	ADD	A,(IY+STRTIPE_PV)
	LD	(IY+STRTIPE_PV),A

	XOR	A
	RET


;NOMBRE: QUIT_OBJ
;OBJETIVO: ELIMINAR LOS BONIFICACIONES DE UN OBJETO AL QUITARLO DEL EQUIPO
;ENTRADA C -> NUMERO DE OBJETO
;         (EQUIPO_ACT) -> VARIABLE QUE INDICA EL EQUIPO DEL PERSONAJE ACTUAL
;         (INV_ACT) -> VARIABLE QUE INDICA EL INVENTARIO DEL PERSONAJE ACTUAL

QUIT_OBJ:	LD	D,0
	LD	E,C
	LD	HL,STRUCTOBJETO
	CALL	MULTHLDE
	LD	DE,OBJ_VEC
	ADD	HL,DE
	LD	DE,STROBJ_LG
	EX	DE,HL
	ADD	HL,DE

	LD	IX,(INV_PERS)
	LD	IY,(INV_TIPO)
	LD	HL,STROBJ_AT
	ADD	HL,DE
	LD	C,(HL)
	LD	A,(IX+STRPER_PVTQ)
	SUB	C
	LD	(IX+STRPER_PVTQ),A

	INC	HL
	LD	C,(HL)
	LD	A,(IX+STRPER_PAR)
	SUB	C
	LD	(IX+STRPER_PAR),A

	INC	HL
	LD	C,(HL)
	LD	A,(IX+STRPER_PMV)
	SUB	C
	LD	(IX+STRPER_PMV),A


	INC	HL
	LD	B,(HL)
	LD	A,(IY+STRTIPE_PV)
	SUB	B
	LD	(IY+STRTIPE_PV),A
	LD	B,(IX+STRPER_PV)
	CP	B
	RET	NC
	LD	(IX+STRPER_PV),A
	RET
